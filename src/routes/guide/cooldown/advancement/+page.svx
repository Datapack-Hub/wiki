---
title: Advancement-based cooldown
description: Learn how to create and check cooldown timers using an advancement and a scoreboard objective.
version: 1.21.11
---

# Advancement-based cooldown

An advancement-based cooldown uses a `minecraft:tick` advancement to run a function while a player is on cooldown. You still use a scoreboard objective to store the remaining time.

Compared to a fully ticking scoreboard approach, this method is **semi-ticking**:

- If a player is **not** on cooldown, nothing runs for them.
- If a player **is** on cooldown, the tick advancement runs a function each tick until the cooldown ends.

This setup takes a little more work because you need an extra advancement, but it can reduce the amount of work your datapack does when most players are not on cooldown.

## What you will build

- A `my_cd` scoreboard objective to store the cooldown value (in ticks)
- A tick advancement (`<namespace>:my_cd`) that runs a function reward
- A `<namespace>:my_cd` function that counts the cooldown down
- A `<namespace>:do_things` function that checks the cooldown before running your action

## 1) Create the tick advancement

Create an advancement that triggers on `minecraft:tick` and runs `<namespace>:my_cd` as a reward.

```json:my_cd.json
{
  "criteria": {
    "tick": {
      "trigger": "minecraft:tick"
    }
  },
  "rewards": {
    "function": "<namespace>:my_cd"
  }
}
```
`minecraft:tick` triggers every tick **until the advancement is granted**.
That detail is what makes this method work: you keep the advancement
“not granted” while the player is on cooldown.

### 2) Create the scoreboard objective.

Create a dummy objective named `my_cd` to store the cooldown value.

 ```mcfunction
 scoreboard objectives add my_cd dummy
 ```
### 3) Implement the cooldown countdown function (`<namespace>:my_cd`)

This function runs once per tick for players who do not have the advancement granted.

- While the cooldown is active (`my_cd` is 1 or higher), revoke the advancement so it stays un-granted and keeps ticking.
- Reduce the score by 1 each tick.

```mcfunction:my_cd.mcfunction
# Reduce the cooldown by 1 tick
scoreboard players remove @s my_cd 1
# Keep the advancement un-granted while the cooldown is active
execute if score @s my_cd matches 1.. run advancement revoke @s only <namespace>:my_cd
```

:::info

If `my_cd` reaches `0`, the `execute if ... matches 1..` check fails,
so the function stops revoking the advancement. After that, the tick advancement
can stay granted, and the reward function will stop running.

:::

### 4) Implement the action function (`<namespace>:do_things`)

This function is what you run when the player performs the action (for example, when they use an item).

It checks the cooldown first. If the player is on cooldown, it stops. Otherwise, it sets a cooldown
and revokes the advancement to start the per-tick countdown.

```mcfunction:do_things.mcfunction
# If the player is on cooldown, stop here
execute if score @s my_cd matches 1.. run return run tellraw @s {"text":"On cooldown!"}
# Set the cooldown to 5 seconds (100 ticks)
scoreboard players set @s my_cd 100
# Start ticking by making the advancement un-granted
advancement revoke @s only <namespace>:my_cd
tellraw @s {"text":"Action performed!"}
```

## Testing

1. Run `/reload`.
2. Run `/function <namespace>:do_things`.
3. Run it again before 5 second passes to confirm it prints `On cooldown!`.
