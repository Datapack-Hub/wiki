---
title: Scoreboard-based cooldown
description: Learn how to create and check cooldown timers using a scoreboard objectives.
version: 1.21.11
---

# Scoreboard-based cooldown

A scoreboard-based cooldown stores a number on each player (or entity) and
changes that number over time.

This guide uses a **ticking** approach, which means the datapack updates the
cooldown every game tick (20 ticks per second). Ticking cooldowns work well
for small projects with only a few cooldowns. If you track many cooldowns or
many entities, ticking can affect performance.

:::tip

To learn how to trigger a function when a player uses an item, see the
[Right-click menu](/guide/right-click) guide.

:::

## What you will build

- A `cooldown` scoreboard objective
- A `tick` function that reduces each player’s cooldown over time
- A `do_things` function that checks the cooldown before doing anything

### 1) Create the scoreboard objective (`load.mcfunction`)

Create a dummy objective named `cooldown`.

A **dummy** objective only changes when commands change it.

```mcfunction:load.mcfunction
scoreboard objectives add cooldown dummy
function <namespace>:tick
```

### 2) Decrease the cooldown every tick (`tick.mcfunction`)

In your `tick` function, schedule the next run, then reduce the `cooldown`
score by `1` for any player who has `cooldown` ≥ 1

```mcfunction:tick.mcfunction
# Run this function again next tick
schedule function <namespace>:tick 1t
# Reduce cooldown for players who are on cooldown
execute as @a[scores={cooldown=1..}] run scoreboard players remove @s cooldown 1
```

### 3) Check the cooldown before running the action (`do_things.mcfunction`)

In the function that performs the action, check the executor’s `cooldown` score.
- If the score is 1 or higher, stop the function and tell the player.
- If the score is 0 (or not set), set a new cooldown value and continue.

```mcfunction:do_things.mcfunction
# If the player is still on cooldown, stop here
execute if score @s cooldown matches 1.. run return run tellraw @s {"text":"On cooldown!"}
# Set the cooldown to 5 seconds (100 ticks)
scoreboard players set @s cooldown 100
tellraw @s {"text":"Action performed!"}
```

## Testing

1. Run `/reload`.
2. Run `/function <namespace>:do_things`.
3. Run it again before 5 second passes to confirm it prints `On cooldown!`.
